buildscript {
    repositories {
		mavenCentral()
        maven {
            name = 'sonatype-nexus-public'
            url = 'https://oss.sonatype.org/content/repositories/public/'
        }
        maven {
            name = 'forge-repo'
            url = 'http://files.minecraftforge.net/maven/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

// Apply Gradle plugins
plugins {
	id 'java'
	id 'eclipse'
	id 'idea'

	id 'com.github.hierynomus.license' version '0.11.0'
	id 'com.github.johnrengelman.shadow' version '1.2.1'
}

apply plugin: 'checkstyle'
apply plugin: 'forge'
apply plugin: 'license'

defaultTasks 'clean', 'licenseFormat', 'build', 'shadowJar'

// Project information
group = 'net.caseif.quartz'
version = '1.8-R0.1'

ext.projectName = 'Quartz'
ext.description = 'A wrapper-based implementation of SpongeAPI.'
ext.inceptionYear = '2015'
ext.packaging = 'jar'
ext.versionSuffix = version.contains('SNAPSHOT') && System.getenv('BUILD_NUMBER') ? '-jnks' + System.getenv('BUILD_NUMBER') : ''

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
	mcVersion = '1.8'
	serverQual = 'net/minecraft/minecraft_server/' + mcVersion
}

// Project repositories
repositories {
	flatDir {
		dirs file(System.getProperty('user.home') + '/.gradle/caches/minecraft/' + serverQual)
	}
    maven {
		// for Mixin
        name = 'minecraft'
        url = 'https://libraries.minecraft.net/'
    }
}

// Project dependencies
dependencies {
	compile project('SpongeAPI')
	compile project('Mixin')
	compile ':minecraft_server:' + mcVersion

	testCompile 'junit:junit:4.12'
	testCompile 'org.hamcrest:hamcrest-library:1.3'
	testCompile 'org.mockito:mockito-core:1.10.19'
	testRuntime 'org.slf4j:slf4j-simple:1.7.10'
}

// MinecraftForge version
minecraft {
    version = "1.8-11.14.0.1295-1.8"
}

// Read source files using UTF-8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

test {
	testLogging {
		showStandardStreams = true
	}
}

jar {
	manifest {
		attributes(
				'Implementation-Title': project.name,
				'Implementation-Version': version,
				'Main-Class': 'net.caseif.quartz.Main',
				'Class-Path': 'lib/' + serverQual + '/minecraft_server-' + mcVersion + '.jar'
		)
	}
	classifier = '-base'
}

shadowJar  {
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    dependencies {
        include(project('SpongeAPI'))
        include(project('Mixin'))
		exclude('net.minecraftforge.gradle:ForgeGradle')
    }
    classifier = ''
    
}
build.dependsOn(shadowJar)

// License header formatting
license {
	header file('LICENSE')
	exclude '*.txt'
	sourceSets = project.sourceSets
	ignoreFailures false
	strictCheck true
	mapping {
		java = 'SLASHSTAR_STYLE'
	}
}

// Disable tests and checkstyle for SpongeAPI
project('SpongeAPI') {
	afterEvaluate { project ->
		configure([assemble, build, compileTestJava, javadoc, javadocJar, sourceJar, licenseFormat]) {
			enabled = false
		}
		tasks.withType(VerificationTask) {
			enabled = false
		}
		tasks.withType(Checkstyle) {
			enabled = false
		}
	}
}
